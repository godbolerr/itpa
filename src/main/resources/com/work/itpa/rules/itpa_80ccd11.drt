template header
id
assessmentYear
ruleTemplate
status
name
sectionType
assesseeType
residentialStatus
hasSalary
minAge
maxAge
maxBasicDAPct

package com.work.itpa.rules;

import java.util.List;
import java.math.BigDecimal;
import com.work.itpa.domain.FinPerson;
import com.work.itpa.domain.Deduction;
import com.work.itpa.domain.FinPersonResult;
import com.work.itpa.domain.Investment;
import com.work.itpa.domain.Person;
import com.work.itpa.domain.Income;

import function com.work.itpa.utils.FiUtil.percentOf;
import function com.work.itpa.utils.FiUtil.lessOf;

template "ATCCD1 Rules - Pension Scheme Contribution "


rule "ATCCD1_1@{row.rowNumber}"
    when
		$fp : FinPerson(assesseeType == "@{assesseeType}" ,	residentialStatus == "@{residentialStatus}" ) 
		
		$fpPerson :  Person   (	age  >= @{minAge} , 
										age  <= @{maxAge} ,
		        		 				relationShipCode == "SELF"  ) from $fp.family 			
		$fpBasicIncome: Income( type == Income.Type.BASIC_SALARY ) from $fp.incomes
		$fpDearnessAllowance: Income ( type == Income.Type.DEARNESS_ALLOWANCE ) from $fp.incomes
		$fpInvestment: Investment ( type == "PS_EMPLOYEE" ) from $fp.investments
		        		 								
    then
    	BigDecimal $allowedDeduction = percentOf($fpBasicIncome.amount.add($fpDearnessAllowance.amount),@{maxBasicDAPct}) ;
    	System.out.println("Allowed deduction is " + $allowedDeduction );
    	Deduction $deduction = new Deduction( 	$fpInvestment.amount,
    											lessOf( $fpInvestment.amount, $allowedDeduction ),
    											$allowedDeduction ,
    											"NA",
    											"@{sectionType}",
    											"Contribution to Pension Scheme" );
    	insert($deduction);
end
end template

